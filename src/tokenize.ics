#import {re} from "std:regex"
#import {s} from "std:strings"

#import {get-line-number-funcs} from "line-numbers"


(* Unicode character classes Lu, Ll, Lt, Lm, Lo, and Nd, plus _ and - *)
IDCHAR = "[-0-9A-Z_a-zÂªÂµÂºÃ€-Ã–Ã˜-Ã¶Ã¸-ËË†-Ë‘Ë -Ë¤Ë¬Ë®Í°-Í´Í¶Í·Íº-Í½Í¿Î†Îˆ-ÎŠÎŒÎ-Î¡Î£-ÏµÏ·-ÒÒŠ-Ô¯Ô±-Õ–Õ™Õ¡-Ö‡×-×ª×°-×²Ø -ÙŠÙ -Ù©Ù®Ù¯Ù±-Û“Û•Û¥Û¦Û®-Û¼Û¿ÜÜ’-Ü¯İ-Ş¥Ş±ß€-ßªß´ßµßºà €-à •à šà ¤à ¨à¡€-à¡˜à¢ -à¢´à¤„-à¤¹à¤½à¥à¥˜-à¥¡à¥¦-à¥¯à¥±-à¦€à¦…-à¦Œà¦à¦à¦“-à¦¨à¦ª-à¦°à¦²à¦¶-à¦¹à¦½à§à§œà§à§Ÿ-à§¡à§¦-à§±à¨…-à¨Šà¨à¨à¨“-à¨¨à¨ª-à¨°à¨²à¨³à¨µà¨¶à¨¸à¨¹à©™-à©œà©à©¦-à©¯à©²-à©´àª…-àªàª-àª‘àª“-àª¨àªª-àª°àª²àª³àªµ-àª¹àª½à«à« à«¡à«¦-à«¯à«¹à¬…-à¬Œà¬à¬à¬“-à¬¨à¬ª-à¬°à¬²à¬³à¬µ-à¬¹à¬½à­œà­à­Ÿ-à­¡à­¦-à­¯à­±à®ƒà®…-à®Šà®-à®à®’-à®•à®™à®šà®œà®à®Ÿà®£à®¤à®¨-à®ªà®®-à®¹à¯à¯¦-à¯¯à°…-à°Œà°-à°à°’-à°¨à°ª-à°¹à°½à±˜-à±šà± à±¡à±¦-à±¯à²…-à²Œà²-à²à²’-à²¨à²ª-à²³à²µ-à²¹à²½à³à³ à³¡à³¦-à³¯à³±à³²à´…-à´Œà´-à´à´’-à´ºà´½àµàµŸ-àµ¡àµ¦-àµ¯àµº-àµ¿à¶…-à¶–à¶š-à¶±à¶³-à¶»à¶½à·€-à·†à·¦-à·¯à¸-à¸°à¸²à¸³à¹€-à¹†à¹-à¹™àºàº‚àº„àº‡àºˆàºŠàºàº”-àº—àº™-àºŸàº¡-àº£àº¥àº§àºªàº«àº­-àº°àº²àº³àº½à»€-à»„à»†à»-à»™à»œ-à»Ÿà¼€à¼ -à¼©à½€-à½‡à½‰-à½¬à¾ˆ-à¾Œá€€-á€ªá€¿-á‰á-á•áš-áá¡á¥á¦á®-á°áµ-á‚á‚á‚-á‚™á‚ -áƒ…áƒ‡áƒáƒ-áƒºáƒ¼-á‰ˆá‰Š-á‰á‰-á‰–á‰˜á‰š-á‰á‰ -áŠˆáŠŠ-áŠáŠ-áŠ°áŠ²-áŠµáŠ¸-áŠ¾á‹€á‹‚-á‹…á‹ˆ-á‹–á‹˜-áŒáŒ’-áŒ•áŒ˜-ášá€-áá -áµá¸-á½á-á™¬á™¯-á™¿áš-áššáš -á›ªá›±-á›¸áœ€-áœŒáœ-áœ‘áœ -áœ±á€-á‘á -á¬á®-á°á€-á³áŸ—áŸœáŸ -áŸ©á -á ™á  -á¡·á¢€-á¢¨á¢ªá¢°-á£µá¤€-á¤á¥†-á¥­á¥°-á¥´á¦€-á¦«á¦°-á§‰á§-á§™á¨€-á¨–á¨ -á©”áª€-áª‰áª-áª™áª§á¬…-á¬³á­…-á­‹á­-á­™á®ƒ-á® á®®-á¯¥á°€-á°£á±€-á±‰á±-á±½á³©-á³¬á³®-á³±á³µá³¶á´€-á¶¿á¸€-á¼•á¼˜-á¼á¼ -á½…á½ˆ-á½á½-á½—á½™á½›á½á½Ÿ-á½½á¾€-á¾´á¾¶-á¾¼á¾¾á¿‚-á¿„á¿†-á¿Œá¿-á¿“á¿–-á¿›á¿ -á¿¬á¿²-á¿´á¿¶-á¿¼â±â¿â‚-â‚œâ„‚â„‡â„Š-â„“â„•â„™-â„â„¤â„¦â„¨â„ª-â„­â„¯-â„¹â„¼-â„¿â……-â…‰â…â†ƒâ†„â°€-â°®â°°-â±â± -â³¤â³«-â³®â³²â³³â´€-â´¥â´§â´­â´°-âµ§âµ¯â¶€-â¶–â¶ -â¶¦â¶¨-â¶®â¶°-â¶¶â¶¸-â¶¾â·€-â·†â·ˆ-â·â·-â·–â·˜-â·â¸¯ã€…ã€†ã€±-ã€µã€»ã€¼ã-ã‚–ã‚-ã‚Ÿã‚¡-ãƒºãƒ¼-ãƒ¿ã„…-ã„­ã„±-ã†ã† -ã†ºã‡°-ã‡¿ã€-ä¶µä¸€-é¿•ê€€-ê’Œê“-ê“½ê”€-ê˜Œê˜-ê˜«ê™€-ê™®ê™¿-êšêš -ê›¥êœ—-êœŸêœ¢-êˆê‹-ê­ê°-ê·êŸ·-ê ê ƒ-ê …ê ‡-ê Šê Œ-ê ¢ê¡€-ê¡³ê¢‚-ê¢³ê£-ê£™ê£²-ê£·ê£»ê£½ê¤€-ê¤¥ê¤°-ê¥†ê¥ -ê¥¼ê¦„-ê¦²ê§-ê§™ê§ -ê§¤ê§¦-ê§¾ê¨€-ê¨¨ê©€-ê©‚ê©„-ê©‹ê©-ê©™ê© -ê©¶ê©ºê©¾-êª¯êª±êªµêª¶êª¹-êª½ê«€ê«‚ê«›-ê«ê« -ê«ªê«²-ê«´ê¬-ê¬†ê¬‰-ê¬ê¬‘-ê¬–ê¬ -ê¬¦ê¬¨-ê¬®ê¬°-ê­šê­œ-ê­¥ê­°-ê¯¢ê¯°-ê¯¹ê°€-í£í°-íŸ†íŸ‹-íŸ»ï¤€-ï©­ï©°-ï«™ï¬€-ï¬†ï¬“-ï¬—ï¬ï¬Ÿ-ï¬¨ï¬ª-ï¬¶ï¬¸-ï¬¼ï¬¾ï­€ï­ï­ƒï­„ï­†-ï®±ï¯“-ï´½ïµ-ï¶ï¶’-ï·‡ï·°-ï·»ï¹°-ï¹´ï¹¶-ï»¼ï¼-ï¼™ï¼¡-ï¼ºï½-ï½šï½¦-ï¾¾ï¿‚-ï¿‡ï¿Š-ï¿ï¿’-ï¿—ï¿š-ï¿œğ€€-ğ€‹ğ€-ğ€¦ğ€¨-ğ€ºğ€¼ğ€½ğ€¿-ğğ-ğğ‚€-ğƒºğŠ€-ğŠœğŠ -ğ‹ğŒ€-ğŒŸğŒ°-ğ€ğ‚-ğ‰ğ-ğµğ€-ğğ -ğƒğˆ-ğğ€-ğ’ğ’ -ğ’©ğ”€-ğ”§ğ”°-ğ•£ğ˜€-ğœ¶ğ€-ğ•ğ -ğ§ğ €-ğ …ğ ˆğ Š-ğ µğ ·ğ ¸ğ ¼ğ ¿-ğ¡•ğ¡ -ğ¡¶ğ¢€-ğ¢ğ£ -ğ£²ğ£´ğ£µğ¤€-ğ¤•ğ¤ -ğ¤¹ğ¦€-ğ¦·ğ¦¾ğ¦¿ğ¨€ğ¨-ğ¨“ğ¨•-ğ¨—ğ¨™-ğ¨³ğ© -ğ©¼ğª€-ğªœğ«€-ğ«‡ğ«‰-ğ«¤ğ¬€-ğ¬µğ­€-ğ­•ğ­ -ğ­²ğ®€-ğ®‘ğ°€-ğ±ˆğ²€-ğ²²ğ³€-ğ³²ğ‘€ƒ-ğ‘€·ğ‘¦-ğ‘¯ğ‘‚ƒ-ğ‘‚¯ğ‘ƒ-ğ‘ƒ¨ğ‘ƒ°-ğ‘ƒ¹ğ‘„ƒ-ğ‘„¦ğ‘„¶-ğ‘„¿ğ‘…-ğ‘…²ğ‘…¶ğ‘†ƒ-ğ‘†²ğ‘‡-ğ‘‡„ğ‘‡-ğ‘‡šğ‘‡œğ‘ˆ€-ğ‘ˆ‘ğ‘ˆ“-ğ‘ˆ«ğ‘Š€-ğ‘Š†ğ‘Šˆğ‘ŠŠ-ğ‘Šğ‘Š-ğ‘Šğ‘ŠŸ-ğ‘Š¨ğ‘Š°-ğ‘‹ğ‘‹°-ğ‘‹¹ğ‘Œ…-ğ‘ŒŒğ‘Œğ‘Œğ‘Œ“-ğ‘Œ¨ğ‘Œª-ğ‘Œ°ğ‘Œ²ğ‘Œ³ğ‘Œµ-ğ‘Œ¹ğ‘Œ½ğ‘ğ‘-ğ‘¡ğ‘’€-ğ‘’¯ğ‘“„ğ‘“…ğ‘“‡ğ‘“-ğ‘“™ğ‘–€-ğ‘–®ğ‘—˜-ğ‘—›ğ‘˜€-ğ‘˜¯ğ‘™„ğ‘™-ğ‘™™ğ‘š€-ğ‘šªğ‘›€-ğ‘›‰ğ‘œ€-ğ‘œ™ğ‘œ°-ğ‘œ¹ğ‘¢ -ğ‘£©ğ‘£¿ğ‘«€-ğ‘«¸ğ’€€-ğ’™ğ’’€-ğ’•ƒğ“€€-ğ“®ğ”€-ğ”™†ğ– €-ğ–¨¸ğ–©€-ğ–©ğ–© -ğ–©©ğ–«-ğ–«­ğ–¬€-ğ–¬¯ğ–­€-ğ–­ƒğ–­-ğ–­™ğ–­£-ğ–­·ğ–­½-ğ–®ğ–¼€-ğ–½„ğ–½ğ–¾“-ğ–¾Ÿğ›€€ğ›€ğ›°€-ğ›±ªğ›±°-ğ›±¼ğ›²€-ğ›²ˆğ›²-ğ›²™ğ€-ğ‘”ğ‘–-ğ’œğ’ğ’Ÿğ’¢ğ’¥ğ’¦ğ’©-ğ’¬ğ’®-ğ’¹ğ’»ğ’½-ğ“ƒğ“…-ğ”…ğ”‡-ğ”Šğ”-ğ””ğ”–-ğ”œğ”-ğ”¹ğ”»-ğ”¾ğ•€-ğ•„ğ•†ğ•Š-ğ•ğ•’-ğš¥ğš¨-ğ›€ğ›‚-ğ›šğ›œ-ğ›ºğ›¼-ğœ”ğœ–-ğœ´ğœ¶-ğğ-ğ®ğ°-ğˆğŠ-ğ¨ğª-ğŸ‚ğŸ„-ğŸ‹ğŸ-ğŸ¿ğ €-ğ£„ğ¸€-ğ¸ƒğ¸…-ğ¸Ÿğ¸¡ğ¸¢ğ¸¤ğ¸§ğ¸©-ğ¸²ğ¸´-ğ¸·ğ¸¹ğ¸»ğ¹‚ğ¹‡ğ¹‰ğ¹‹ğ¹-ğ¹ğ¹‘ğ¹’ğ¹”ğ¹—ğ¹™ğ¹›ğ¹ğ¹Ÿğ¹¡ğ¹¢ğ¹¤ğ¹§-ğ¹ªğ¹¬-ğ¹²ğ¹´-ğ¹·ğ¹¹-ğ¹¼ğ¹¾ğº€-ğº‰ğº‹-ğº›ğº¡-ğº£ğº¥-ğº©ğº«-ğº»ğ €€-ğª›–ğªœ€-ğ«œ´ğ«€-ğ« ğ«  -ğ¬º¡ğ¯ €-ğ¯¨]";


binops = new-set(s.split("== != < > <= >= - + * -. +. *. / % ** ^ | & << >> >>> && || +' <' >' <=' >='", " "));
literals = new-set(s.split("( ) { } [ ] : , . = ; ! - # {- -}", " "));

special = sorted(new-set([...binops, ...literals]), funct (a, b) s.len(b) - s.len(a) end);

do unsafe
WS-RE = re.new("^\s+");
PID-RE = re.new("^@[@\w\-]+");
JS-RE = re.new("^`[^`]*`");
STR-RE = re.new(R"(^"[^"]*")");
ID-RE = re.new-with-flags("^" +' IDCHAR +' "+", "u");
LIT-RE = re.new("^(?:" +'
    map(special, funct(lit) "\" +' s.replace(lit, "", "\") end).join("|")
    +' ")");
POLY-RE = re.new("^\$+");
end;


wordset = new-set(s.split(
    "do if while for in then else end funct let future mut case import unsafe from language", " "));



let Span future = funct(fname, source, pos, len) {
    fname, source, pos,
    pos2: pos + len,

    merge: funct this (other)
        if other != null then
            pos = min(this.pos, other.pos);
            pos2 = max(this.pos2, other.pos2);
            Span(this.fname, this.source, pos, pos2 - pos)
        else
            this
        end
    end,

    start: funct this ()
        Span(this.fname, this.source, this.pos, 0)
    end,

    text: funct this ()
        s.slice(this.source, this.pos, this.pos2)
    end,

    print: funct this (message)
        MAXLINELEN = 80;
        HALFLINE = int(MAXLINELEN/2);

        let {fname, source, pos, pos2} = this;
        funcs = get-line-number-funcs(source);

        start = funcs.line-start(pos);
        line-start = start;

        let (end_, lineno) = funcs.line-end-and-count(start);

        (* Find an 80 char section of the line around the point of interest to display *)
        temp = min(pos2, pos + HALFLINE);
        if temp < (start + MAXLINELEN).0 then
            end_ = min(end_, start + MAXLINELEN);
        else if pos >= (end_ - MAXLINELEN).0 then
            start = max(start, end_ - MAXLINELEN);
        else
            mid = int((pos + temp).0 / 2);
            start = max(start, mid - HALFLINE);
            end_ = min(end_, start + MAXLINELEN);
        end end;

        pos2 = min(pos2, end_);
        col = pos - line-start + 1; (* column numbering starts at 1 *)
        snippet = s.slice(source, start, end_);
        markers =
            s.repeat(" ", pos - start) +'
            "^" +' s.repeat("-", max(pos2 - pos - 1, 0)) +'
            s.repeat(" ", end_ - pos2);

        fname +' ":" +' lineno +' ":" +' col +' ": " +' message +' NL +' snippet +' NL +' markers
    end,
} end;

Token = funct(type, val, span) {type, val, span} end;

tokenize = funct(fname, source, include-comments)
    results = eml();
    error = null;

    src = source;
    pos = 0;

    do while error == null && (s.len(src) > 0).0 then
        val = null;
        if s.startsWith(src, "(*") then
            level = 1;
            p1 = 2;
            p2 = -1; (* will be initialized in loop *)

            do while level > 0 then
                temp = s.indexOf(s.sliceE(src, p1), "*)");
                if temp == -1 then
                    start = pos + p1 - 2;
                    span = Span(fname, source, start, s.len(source) - start);
                    error = span.print("SyntaxError: Unclosed comment");
                    p2 = s.len(src); level = 0;
                else
                    p2 = p1 + temp + 2;
                    level = level + s.split(s.slice(src, p1, p2), "(*").length - 2;
                    p1 = p2;
                end;
            end;

            type = "COMMENT";
            m = (s.slice(src, 0, p2));
        else if s.startsWith(src, R"(R")") then
            delim-end = s.indexOf(src, "(");
            if delim-end == -1 then
                error = Span(fname, source, pos, s.len(source) - pos).print("SyntaxError: Unclosed raw string delimiter");
                type = "WHITESPACE";
                m = ("");
            else
                delim = ")" +' s.slice(src, 2, delim-end) +' s.QUOTE;
                string-start = delim-end + 1; (* opening paren takes 1 *)
                string-end = s.indexOf(src, delim);
                if string-end == -1 then
                    error = Span(
                        fname, source, string-start, s.len(source) - string-start
                    ).print("SyntaxError: Unclosed raw string");
                    type = "WHITESPACE";
                    m = ("");
                else
                    type = "STRING";
                    m = (s.slice(src, 0, string-end + s.len(delim)));
                    val = s.slice(src, string-start, string-end);
                end;
            end;
        else if m = s.match(src, WS-RE); m != null then
            type = "WHITESPACE";
        else if m = s.match(src, JS-RE); m != null then
            type = "JS";
        else if m = s.match(src, STR-RE); m != null then
            type = "STRING";
            val = s.slice(m.0, 1, -1)
        else if m = s.match(src, ID-RE); m != null && (m.0 != "-").0 then
            type = "ID";
            if wordset.has(m.0) then
                type = s.toUpperCase(m.0);
            end;

            (* Check if token is all dashes, in which case it wasn't a valid identifier after all *)
            if m.0 == s.repeat("-", s.len(m.0)) then
                type = "ERROR";
            end;

        else if m = s.match(src, PID-RE); m != null then
            type = "PHANTOM_ID";
        else if m = s.match(src, LIT-RE); m != null then
            type = m.0;
            if binops.has(m.0) && !literals.has(m.0) then
                type = "BINOP";
            end;
        else if m = s.match(src, POLY-RE); m != null then
            type = "POLY";
        else
            type = "ERROR";
            m = (src)
        end end end end end end end end end;

        token-text = m.0;
        token-start = pos;
        token-len = s.len(token-text);
        if val == null then
            val = token-text;
        end;

        pos = pos + token-len;
        src = s.sliceE(src, token-len);

        if type != "WHITESPACE" && (type != "COMMENT" || include-comments).0 then
            (* automatic semicolon deletion *)
            if token-text == "end" || (token-text == "else").0 then
                do while last = results.last(); last != _ && (last.type == ";").0 then
                    results.pop();
                end;
            end;

            token = Token(type, val, Span(fname, source, token-start, token-len));
            results.push(token);

            if type == "ERROR" then
                error = token.span.print("SyntaxError: Invalid token");
            end;
        end;
    end;

    (* automatic semicolon deletion *)
    do while last = results.last(); last != _ && (last.type == ";").0 then
        results.pop();
    end;
    results.push(Token("EOF", "", Span(fname, source, s.len(source), 0)));

    (* reverse token list so we can pop efficiently *)
    tokens = eml();
    do while tok = results.pop(); tok != _ then
        tokens.push(tok);
    end;

    if error != null then
        case Err(error)
    else
        case Ok(tokens)
    end
end;
